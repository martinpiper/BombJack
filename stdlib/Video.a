!zn
+MCheckNotInIO

!ifdef kBus24Bit_VideoLayer_VBlankUseNMI {
.setVBlankNMI !by 0
.isVBlankNMI !by 0
Video_WaitVBlank_FramesPassed !by 0
Video_WaitVBlank
	lda .setVBlankNMI
	bne .isSetNMI

	+MByteValueToAddress_A 1 , .setVBlankNMI
	+MByteValueToAddress_A $7f , CIA2InterruptControl
	+MWordValueToAddress_A .nmi , KERNALNMIServiceRoutineLo
	+MByteValueToAddress_A %10010000 , CIA2InterruptControl

.isSetNMI
	; ACK Any previous VBLANK
	lda #0
	sta .isVBlankNMI
;Video_WaitVBlank_startGuard
.l1
	lda .isVBlankNMI
	beq .l1
;Video_WaitVBlank_endGuard
	rts

.nmi
	pha
	+MByteValueToAddress_A 1 , .isVBlankNMI
	inc Video_WaitVBlank_FramesPassed
	lda ZPProcessorPort
	pha
	+MByteValueToAddress_A ProcessorPortAllRAMWithIO , ZPProcessorPort
	; ACK NMI VBlank
	lda CIA2InterruptControl
	pla
	sta ZPProcessorPort
	pla
	rti

} else {

; Waits for the start of the _VBLANK to be triggered from the video, which is a negative edge on the _VBLANK line
Video_WaitVBlank
	; ACK Any previous VBLANK
	lda CIA2InterruptControl

	; Wait for VBLANK event
	lda #%10000
	; Can be used with: When ignore address Video_WaitVBlank_startGuard to Video_WaitVBlank_endGuard for trace
Video_WaitVBlank_startGuard
.l1
	bit CIA2InterruptControl
	beq .l1
Video_WaitVBlank_endGuard
	rts
}




Video_DisableDisplay
	jsr Video_SpritesDisable
	; Reset priority to default
	jsr Video_SetAddressVideoPriorityRegister
	lda #kBus24Bit_VideoLayer_Priority_Default
	sta CIA2PortBRS232
	; Disable display entirely
	jsr Video_SetAddressVideoControlRegisters
	lda #$00
	sta CIA2PortBRS232
!ifdef kBus24Bit_VideoLayer_ExpandedPalettes {
	jsr Video_SetAddressVideoPaletteBankRegister
	jsr Video_clearByte
}
	rts


; A = Value to use for the video initialisation: kVideo_EnableDisplay_*
Video_EnableDisplay
	pha
	jsr Video_SetAddressVideoControlRegisters
	pla
	sta CIA2PortBRS232
	rts


; Clears sprite registers, turns off sprites, no 32x32 sprites
Video_SpritesDisable
	jsr Video_SetAddressSpritesControl
	lda #0
	sta CIA2PortBRS232
	sta CIA2PortBRS232

	jsr Video_SetAddressSprites
	lda #0
	ldx #kBus24Bit_SpritesMaxNum*4
.cs1
	sta CIA2PortBRS232
	dex
	bne .cs1
	rts

	
; A/X = lo/hi address
; Y = length
Video_SpriteDataCopy
	pha
	jsr Video_SetAddressSprites
	pla
	jmp Bus24Bit_CopySmallData


Video_SetAddressVideoControlRegisters
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_Control
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_Control
	sta CIA2PortBRS232
	rts

!ifdef kBus24Bit_VideoLayer_ExpandedPalettes {
Video_SetAddressVideoPaletteBankRegister
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_PaletteBank
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_PaletteBank
	sta CIA2PortBRS232
	rts
}

!ifdef kBus24Bit_VideoLayer_ExpandedPaletteLayers {
Video_SetAddressVideoPaletteLayersRegister
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_PaletteLayers
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_PaletteLayers
	sta CIA2PortBRS232
	rts
}

!ifdef kBus24Bit_VideoLayer_HasOverscan {
Video_SetAddressVideoOverscanExtentRegisters
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_OverscanExtent
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_OverscanExtent
	sta CIA2PortBRS232
	rts
}

Video_SetAddressVideoPriorityRegister
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_Priority
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_Priority
	sta CIA2PortBRS232
	rts


Video_SetAddressTileScrollRegisters
	jsr Bus24Bit_Reset
	lda #kBus24Bit_TileScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_TileScreen_ScrollX16
	sta CIA2PortBRS232
	lda #>kBus24Bit_TileScreen_ScrollX16
	sta CIA2PortBRS232
	rts


Video_SetAddressTileBackgroundRegister
	jsr Bus24Bit_Reset
	lda #kBus24Bit_TileScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_TileScreen_Background
	sta CIA2PortBRS232
	lda #>kBus24Bit_TileScreen_Background
	sta CIA2PortBRS232
	rts


Video_SetAddressPalette
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_Palette
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_Palette
	sta CIA2PortBRS232
	rts

; Clears colour 0 in all palettes to the same input colour
; Entry:
; A/X = RGB 2 bytes
Video_PalettesColour0ClearTo
	sta .smPE1+1
	stx .smPE2+1
	lda #<kBus24Bit_VideoLayer_Palette
	sta .smAddrP1+1
	lda #>kBus24Bit_VideoLayer_Palette
	sta .smAddrP2+1
	ldx #16
.spl1
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
.smAddrP1	lda #0
	sta CIA2PortBRS232
.smAddrP2	lda #0
	sta CIA2PortBRS232
.smPE1	lda #0
	sta CIA2PortBRS232
.smPE2	lda #0
	sta CIA2PortBRS232
	+MAddU8ToAddr16 16*2 , .smAddrP1+1 , .smAddrP2+1
	dex
	bne .spl1

	rts



Video_SetAddressSprites
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Sprites_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_Sprites
	sta CIA2PortBRS232
	lda #>kBus24Bit_Sprites
	sta CIA2PortBRS232
	rts


Video_SetAddressSpritesControl
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Sprites_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_SpritesControlLo
	sta CIA2PortBRS232
	lda #>kBus24Bit_SpritesControlLo
	sta CIA2PortBRS232
	rts


!zn
Video_SetAddressSprites2
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Sprites2_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_Sprites2
	sta CIA2PortBRS232
	lda #>kBus24Bit_Sprites2
	sta CIA2PortBRS232
	rts


Video_SetAddressMode7
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Mode7Screen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_Mode7Screen_Registers
	sta CIA2PortBRS232
	lda #>kBus24Bit_Mode7Screen_Registers
	sta CIA2PortBRS232
	rts


Video_SetAddressCharsControl
	jsr Bus24Bit_Reset
	lda #kBus24Bit_CharScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_CharScreen_Control
	sta CIA2PortBRS232
	lda #>kBus24Bit_CharScreen_Control
	sta CIA2PortBRS232
	rts


Video_SetAddressCharScrollRegisters
	jsr Bus24Bit_Reset
	lda #kBus24Bit_CharScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_CharScreen_ScrollX16
	sta CIA2PortBRS232
	lda #>kBus24Bit_CharScreen_ScrollX16
	sta CIA2PortBRS232
	rts

!ifdef kBus24Bit_VideoLayer_HasOverscan {
Video_SetAddressVideoLayersEnable
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_LayersEnable
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_LayersEnable
	sta CIA2PortBRS232
	rts

Video_SetAddressVideoBackgroundColour
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_BackgroundColour
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_BackgroundColour
	sta CIA2PortBRS232
	rts
}

Video_SetAddressMergeLayer
	jsr Bus24Bit_Reset
	lda #kBus24Bit_MergeLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_MergeLayer_Registers
	sta CIA2PortBRS232
	lda #>kBus24Bit_MergeLayer_Registers
	sta CIA2PortBRS232
	rts


; Usually call this immediately after using Video_WaitVBlank
Video_StartRasterTimers
	; Start two timers, which should count raster position
	lda #0
	sta CIA1TimerAControl
	sta CIA1TimerBControl

	+MWordValueToAddress_A CyclesPerSecondPALC64 / 15750 , CIA1TimerALo
	+MWordValueToAddress_A 256+6 , CIA1TimerBLo	; Note the tweak value here uses 9 bits

	lda #%01011011
	sta CIA1TimerBControl
	lda #%00010111
	sta CIA1TimerAControl
	rts

Video_clearByte
	lda #$00
	sta CIA2PortBRS232
	rts

Video_clearThreeBytes
	lda #$00
	sta CIA2PortBRS232
	sta CIA2PortBRS232
	sta CIA2PortBRS232
	rts

Video_clearFourBytes
	lda #$00
	sta CIA2PortBRS232
	sta CIA2PortBRS232
	sta CIA2PortBRS232
	sta CIA2PortBRS232
	rts

; Initialises sprites without any large sprites, tiles/char screen scrolls and tiles background colour to 0
; Most useful when called after Video_DisableDisplay
Video_InitDisplaySpritesScrollsBackground
!ifdef kBus24Bit_VideoLayer_HasOverscan {
	jsr Video_SetAddressVideoOverscanExtentRegisters
	lda #kBus24Bit_VideoLayer_OverscanExtent_Default
	sta CIA2PortBRS232
	lda #kBus24Bit_VideoLayer_LayersEnable_Default
	sta CIA2PortBRS232
	lda #kBus24Bit_VideoLayer_BackgroundColour_Default
	sta CIA2PortBRS232
!ifdef kBus24Bit_VideoLayer_ExpandedPalettes {
	jsr Video_clearByte
}
}

	; Sprites enable, no large sprites
	jsr Video_SetAddressSpritesControl
!ifndef kBus24Bit_VideoLayer_HasOverscan {
	lda #kBus24Bit_SpritesControlLoEnable
} else {
	lda #0
}
	sta CIA2PortBRS232
	jsr Video_clearByte

	; Default merge layer behaviour
	jsr Video_SetAddressMergeLayer
	lda #kBus24Bit_MergeLayer_Register_Control_Default
	sta CIA2PortBRS232
	lda #0
	sta CIA2PortBRS232
	lda #kBus24Bit_MergeLayer_Register_Control_Default
	sta CIA2PortBRS232
	lda #0
	sta CIA2PortBRS232

	jsr Video_SetAddressCharsControl
	jsr Video_clearByte
	; Scrolls
	jsr Video_clearFourBytes

	jsr Video_SetAddressTileScrollRegisters
	; Scrolls
	jsr Video_clearFourBytes

	jsr Video_SetAddressTileBackgroundRegister
	jsr Video_clearByte

	jsr Video_SetAddressSprites2
	jsr Video_clearFourBytes

	rts

+MCheckNotInIO
