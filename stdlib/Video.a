!zn
; Waits for the start of the _VBLANK to be triggered from the video, which is a negative edge on the _VBLANK line
Bus24Bit_WaitVBlank
	; ACK Any previous VBLANK
	lda CIA2InterruptControl

	; Wait for VBLANK event
	lda #%10000
.l1
	bit CIA2InterruptControl
	beq .l1
	rts


Bus24Bit_DisableDisplay
	jsr Bus24Bit_SetAddressVideoControlRegisters
	lda #$00
	sta CIA2PortBRS232
	rts


kBus24Bit_EnableDisplay_Tiles_Enable		= $10
kBus24Bit_EnableDisplay_Enable				= $20
kBus24Bit_EnableDisplay_Tiles_BorderXShrink	= $40
kBus24Bit_EnableDisplay_Tiles_BorderYShrink	= $80

; A = Value to use for the video initialisation
Bus24Bit_EnableDisplay
	pha
	jsr Bus24Bit_SetAddressVideoControlRegisters
	pla
	sta CIA2PortBRS232
	rts


; Clears sprite registers, turns off sprites, no 32x32 sprites
Bus24Bit_SpritesDisable
	jsr Bus24Bit_SetAddressSpritesControl
	lda #0
	sta CIA2PortBRS232
	sta CIA2PortBRS232

	jsr Bus24Bit_SetAddressSprites
	lda #0
	ldx #24*4
.cs1
	sta CIA2PortBRS232
	dex
	bne .cs1
	rts

	
; A/X = lo/hi address
; Y = length
Bus24Bit_SpriteDataCopy
	pha
	jsr Bus24Bit_SetAddressSprites
	pla
	jmp Bus24Bit_CopySmallData


Bus24Bit_SetAddressVideoControlRegisters
	jsr Bus24Bit_Reset
	lda #$01; EBBS
	sta CIA2PortBRS232
	lda #$00
	sta CIA2PortBRS232
	lda #$9e
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressTileScrollRegisters
	jsr Bus24Bit_Reset
	lda #$01; EBBS
	sta CIA2PortBRS232
	lda #$01
	sta CIA2PortBRS232
	lda #$9e
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressTileBackgroundRegister
	jsr Bus24Bit_Reset
	lda #$01; EBBS
	sta CIA2PortBRS232
	lda #$07
	sta CIA2PortBRS232
	lda #$9e
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressPalette
	jsr Bus24Bit_Reset
	lda #$01; EBBS
	sta CIA2PortBRS232
	lda #$00
	sta CIA2PortBRS232
	lda #$9c
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressSprites
	jsr Bus24Bit_Reset
	lda #$01; EBBS
	sta CIA2PortBRS232
	lda #$20
	sta CIA2PortBRS232
	lda #$98
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressSpritesControl
	jsr Bus24Bit_Reset
	lda #$01; EBBS
	sta CIA2PortBRS232
	lda #$00
	sta CIA2PortBRS232
	lda #$9a
	sta CIA2PortBRS232
	rts


; Usually call this immediately after using Bus24Bit_WaitVBlank
Bus24Bit_StartRasterTimers
	; Start two timers, which should count raster position
	lda #0
	sta CIA1TimerAControl
	sta CIA1TimerBControl

	+MWordValueToAddress_A CyclesPerSecondPALC64 / 15750 , CIA1TimerALo
	+MWordValueToAddress_A 256+6 , CIA1TimerBLo	; Note the tweak value here uses 9 bits

	lda #%01011011
	sta CIA1TimerBControl
	lda #%00010111
	sta CIA1TimerAControl
	rts
