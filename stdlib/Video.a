!zn
; Waits for the start of the _VBLANK to be triggered from the video, which is a negative edge on the _VBLANK line
Bus24Bit_WaitVBlank
	; ACK Any previous VBLANK
	lda CIA2InterruptControl

	; Wait for VBLANK event
	lda #%10000
.l1
	bit CIA2InterruptControl
	beq .l1
	rts


Bus24Bit_DisableDisplay
	; Reset priority to default
	jsr Bus24Bit_SetAddressVideoPriorityRegister
	lda #%11100100
	sta CIA2PortBRS232
	; Disable display entirely
	jsr Bus24Bit_SetAddressVideoControlRegisters
	lda #$00
	sta CIA2PortBRS232
	rts


; A = Value to use for the video initialisation
Bus24Bit_EnableDisplay
	pha
	jsr Bus24Bit_SetAddressVideoControlRegisters
	pla
	sta CIA2PortBRS232
	rts


; Clears sprite registers, turns off sprites, no 32x32 sprites
Bus24Bit_SpritesDisable
	jsr Bus24Bit_SetAddressSpritesControl
	lda #0
	sta CIA2PortBRS232
	sta CIA2PortBRS232

	jsr Bus24Bit_SetAddressSprites
	lda #0
	ldx #24*4
.cs1
	sta CIA2PortBRS232
	dex
	bne .cs1
	rts

	
; A/X = lo/hi address
; Y = length
Bus24Bit_SpriteDataCopy
	pha
	jsr Bus24Bit_SetAddressSprites
	pla
	jmp Bus24Bit_CopySmallData


Bus24Bit_SetAddressVideoControlRegisters
	jsr Bus24Bit_Reset
	lda #kBus24Bit_TileScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_TileScreen_EnableBorders
	sta CIA2PortBRS232
	lda #>kBus24Bit_TileScreen_EnableBorders
	sta CIA2PortBRS232
	rts



Bus24Bit_SetAddressVideoPriorityRegister
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_Priority
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_Priority
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressTileScrollRegisters
	jsr Bus24Bit_Reset
	lda #kBus24Bit_TileScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_TileScreen_ScrollX16
	sta CIA2PortBRS232
	lda #>kBus24Bit_TileScreen_ScrollX16
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressTileBackgroundRegister
	jsr Bus24Bit_Reset
	lda #kBus24Bit_TileScreen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_TileScreen_Background
	sta CIA2PortBRS232
	lda #>kBus24Bit_TileScreen_Background
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressPalette
	jsr Bus24Bit_Reset
	lda #kBus24Bit_VideoLayer_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_VideoLayer_Palette
	sta CIA2PortBRS232
	lda #>kBus24Bit_VideoLayer_Palette
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressSprites
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Sprites_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_Sprites
	sta CIA2PortBRS232
	lda #>kBus24Bit_Sprites
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressSpritesControl
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Sprites_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_SpritesControlLo
	sta CIA2PortBRS232
	lda #>kBus24Bit_SpritesControlLo
	sta CIA2PortBRS232
	rts


Bus24Bit_SetAddressMode7
	jsr Bus24Bit_Reset
	lda #kBus24Bit_Mode7Screen_EBBS
	sta CIA2PortBRS232
	lda #<kBus24Bit_Mode7Screen_Registers
	sta CIA2PortBRS232
	lda #>kBus24Bit_Mode7Screen_Registers
	sta CIA2PortBRS232
	rts


; Usually call this immediately after using Bus24Bit_WaitVBlank
Bus24Bit_StartRasterTimers
	; Start two timers, which should count raster position
	lda #0
	sta CIA1TimerAControl
	sta CIA1TimerBControl

	+MWordValueToAddress_A CyclesPerSecondPALC64 / 15750 , CIA1TimerALo
	+MWordValueToAddress_A 256+6 , CIA1TimerBLo	; Note the tweak value here uses 9 bits

	lda #%01011011
	sta CIA1TimerBControl
	lda #%00010111
	sta CIA1TimerAControl
	rts
